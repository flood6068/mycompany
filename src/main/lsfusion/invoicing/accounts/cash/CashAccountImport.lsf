MODULE CashAccountImport;

REQUIRE CashAccount, Partner;

NAMESPACE Invoicing;

cashAccountExport 'Экспорт касс' () {
    NEWSESSION {
        LOCAL f = EXCELFILE ();
        EXPORT XLSX HEADER FROM 'Код контрагента' = id(holder(CashAccount c)), 'Примечание' = note(c),
                                'Наименование' = name(c) WHERE c IS CashAccount TO f;
        open(f());
    }
}

cashAccountImport 'Импорт касс' () {
    LOCAL idPartner = STRING[50] (INTEGER);
    LOCAL note = ISTRING[100] (INTEGER);
    LOCAL name = ISTRING[100] (INTEGER);
    
    INPUT f = EXCELFILE DO {
        NEWSESSION {
            IMPORT XLS HEADER FROM f TO idPartner = A, note = B, name = C;

            FOR [GROUP SUM 1 BY idPartner(INTEGER i)](num) AND NOT partner[STRING](num) DO {
                MESSAGE 'Отсутствует контрагент в базе данных с кодом ' + num + '. Импорт отменен';
                RETURN;
            } 
            
            FOR imported(INTEGER i)  AND
                partner(idPartner(i))
                DO NEW c = CashAccount {
                    holder(c) <- partner(idPartner(i));
                    note(c) <- note(i);
                    name(c) <- name(i); 
            }
            
            APPLY;
            MESSAGE 'Импорт завершен';
        }
    }
}    

EXTEND FORM migrationData 
    PROPERTIES cashAccountExport(), cashAccountImport()
;

DESIGN migrationData {
    commons {
        NEW cashAccount {
            type = CONTAINERH;
            caption = 'Кассы';
            MOVE PROPERTY(cashAccountImport());
            MOVE PROPERTY(cashAccountExport());
        }
    }
}
