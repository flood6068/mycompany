MODULE MetaTax;

META defineTaxCalc(doc, let, prefix, price)
    untaxedAmount 'Сумма'  = DATA NUMERIC[14,2] (###doc##Line);
    
    WHEN LOCAL CHANGED(quantity(###doc##Line l)) OR CHANGED(price(l)) DO {
        untaxedAmount(l) <- NUMERIC[14,2](quantity(l) * price(l));
    }
    
    // taxes for line
    in 'Вкл' = DATA BOOLEAN (###doc##Line, Tax);
    WHEN LOCAL CHANGED(item(###doc##Line l)) DO
        in(l, Tax t) <- prefix##In(item(l), t);

    changeTax (###doc##Line l) {
        REQUEST {
            setTax (Tax t) <- in(l, t);
            DIALOG changeTax; 
        } DO in(l, Tax t) <- setTax(t);
    } 
     
    // amount    
    taxAmount 'Налоги' (Tax t, ###doc##Line l) = DATA NUMERIC[14,2] (Tax, ###doc##Line);
    
    taxIncluded = ABSTRACT BOOLEAN (###doc##Line);
    WHEN LOCAL CHANGED(untaxedAmount(###doc##Line l) IF in(l, Tax t)) DO
        taxAmount (t, l) <- NUMERIC[14,2] (value(t) / (100 (+) (value(t) IF taxIncluded(l))) * untaxedAmount(l));
    
    CONSTRAINT (DROPPED(Tax t IS Tax) AND PREV(taxAmount(t, ###doc##Line l))) 
                                                            MESSAGE 'Нельзя удалять налог, участвующий в расчетах';
    
    taxAmount 'Налоги' (###doc##Line l) = GROUP SUM taxAmount(Tax t, l) IF in(l, t);
    taxPercent 'Налоги, %' (###doc##Line l) = GROUP SUM value(Tax t) IF in(l, t);
    taxes 'Налоги' (###doc##Line l) = GROUP CONCAT name(Tax t) IF in(l, t), ', ' ORDER t CHARWIDTH 8;

    amount 'Итоговая сумма' (###doc##Line l) = untaxedAmount(l) (+) (taxAmount(l) IF NOT taxIncluded(l));
    
    // taxes for document
    taxCount (###doc let, Tax t) = GROUP SUM 1 IF in(###doc##Line l, t) AND doc(l) = let;
    taxAmount 'Налог' (Tax t, ###doc let) = 
        GROUP SUM taxAmount(t, ###doc##Line l) IF doc(l) = let AND in(l, t);
    
    untaxedAmount 'Сумма' (###doc let) = GROUP SUM untaxedAmount(###doc##Line l) IF doc(l) = let;

    taxAmount 'Налог' (###doc let) = GROUP SUM taxAmount(Tax t, let);

    amount 'Итоговая сумма' (###doc let) = GROUP SUM amount(###doc##Line l) IF doc(l) = let;
END

META defineTaxCalc(doc, let, prefix)
    @defineTaxCalc(doc, let, prefix, price);
END

META defineTaxForm(doc, let)
    EXTEND FORM doc  
        PROPERTIES(l) untaxedAmount
            
        OBJECTS t = Tax
        PROPERTIES name(t) READONLY, taxAmount(t, let) READONLY, taxAmount(t, l)
        FILTERS taxCount(let, t)
                
        PROPERTIES(let) untaxedAmount, taxAmount, amount
    ;
        
    DESIGN doc {
        linesFooter {
            NEW taxes {           
                fill = 1;
                MOVE BOX(t) { 
                    caption = 'Налоги'; 
                    GRID(t) { headerHeight = 24; } 
                }
                REMOVE TOOLBARBOX(t);
            }
            NEW total {
                caption = 'Итого по документу';
                MOVE PROPERTY(untaxedAmount(let));                
                MOVE PROPERTY(taxAmount(let));                
                MOVE PROPERTY(amount(let));                
            }
        }
    }
        
    EXTEND FORM doc##s 
        PROPERTIES(let) READONLY untaxedAmount, taxAmount, amount     
    ;
END

