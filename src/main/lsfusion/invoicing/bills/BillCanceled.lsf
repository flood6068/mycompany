MODULE BillCanceled;

REQUIRE BillReady;

NAMESPACE Invoicing;
    
EXTEND CLASS BillStatus {
    canceled 'Отменен'
}
canceled 'Отменен' = DATA BOOLEAN (Bill);
active 'Активен' (Bill b) = ready(b) AND NOT canceled(b);

status(Bill b) += WHEN canceled(b) THEN BillStatus.canceled;
colorStatus(Bill b) += WHEN status(b) = BillStatus.canceled THEN RGB(181,195,247);

cancel 'Отменить' (Bill b) {
    APPLY; 
       IF canceled() THEN RETURN;
           
       NEWSESSION {
           canceled(b) <- TRUE;
           APPLY;
       }
}

EXTEND FORM bill
    PROPERTIES(b) cancel SHOWIF NOT (status(b) = BillStatus.draft OR status(b) = BillStatus.canceled), canceled
;

DESIGN bill {
    secondaryActions {
        MOVE PROPERTY(cancel(b));
    }             
    status {
        MOVE PROPERTY(canceled(b));                               
    }    
}

EXTEND FORM bills    
    EXTEND FILTERGROUP status
        FILTER 'Отменен' status(b) = BillStatus.canceled    
;

@defineSelectionAction(bills, Bill, cancel, 'Отменить', statuses);