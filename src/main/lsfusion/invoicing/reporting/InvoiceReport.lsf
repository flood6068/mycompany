MODULE InvoiceReport;

REQUIRE CreditNote, InvoiceDebt, ItemAttribute;

NAMESPACE Invoicing;

attribute(InvoiceLine l, ItemAttribute a) = value(attribute(item(l), a));

FORM invoiceReport 'Отчет по реализациям'
    OBJECTS a = ItemAttribute

    OBJECTS report = (i = Invoice, l = InvoiceLine) PIVOT
    PROPERTIES(i) READONLY nameType, dateTime, nameStatus, number, 
                           nameCustomer, nameCustomerAccount, nameCompany, nameCompanyAccount, 
                           namePaymentTerms, dueDateTime, note
    PROPERTIES(l) READONLY nameItem, idItem
    PROPERTIES READONLY 'Группа 1' = level1(item(l)), 'Группа 2' = level2(item(l)), 'Группа 3' = level3(item(l)), 'Группа 4' = level4(item(l)), 
                        'Группа полная' = canonicalNameCategory(item(l)) 
    PROPERTIES READONLY attribute(l, a) COLUMNS (a) HEADER name(a)
    PROPERTIES(l) READONLY quantity, price, taxes
    PROPERTIES(l) READONLY MEASURE
                           untaxedAmount, taxAmount, amount
    FILTERS invoice(l) = i
;

@defineDateTimeAggregationForm(invoiceReport, i);
@defineDateFilterForm(invoiceReport, i);

NAVIGATOR {
    invoicing {
        reporting {
            NEW invoiceReport;        
        }
    }
}