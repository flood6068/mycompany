MODULE InvoiceCanceled;

REQUIRE InvoiceReady;

NAMESPACE Invoicing;
    
EXTEND CLASS InvoiceStatus {
    canceled 'Отменен'
}
canceled 'Отменен' = DATA BOOLEAN (Invoice);
active 'Активен' (Invoice i) = ready(i) AND NOT canceled(i);

status(Invoice i) += WHEN canceled(i) THEN InvoiceStatus.canceled;
colorStatus(Invoice i) += WHEN status(i) = InvoiceStatus.canceled THEN RGB(181,195,247);

cancel 'Отменить' (Invoice i) {
    APPLY; 
       IF canceled() THEN RETURN;
           
       NEWSESSION {
           canceled(i) <- TRUE;
           APPLY;
       }
}

EXTEND FORM invoice
    PROPERTIES(i) cancel SHOWIF NOT (status(i) = InvoiceStatus.draft OR status(i) = InvoiceStatus.canceled), canceled 
;

DESIGN invoice {
    secondaryActions {
        MOVE PROPERTY(cancel(i));
    }             
    status {
        MOVE PROPERTY(canceled(i));                               
    }    
}

EXTEND FORM invoices    
    EXTEND FILTERGROUP status
        FILTER 'Отменен' status(i) = InvoiceStatus.canceled
;

@defineSelectionAction(invoices, Invoice, cancel, 'Отменить', statuses);