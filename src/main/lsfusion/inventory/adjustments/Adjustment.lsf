MODULE Adjustment;

REQUIRE InventorySettings, LocationEmployee, 
        Product, InvLedger, DateUtils,
        Company, BarCode, Doc;
                                  
NAMESPACE Inventory;

CLASS Adjustment 'Инвентаризация';

@defineDocStatus(adjustment, 'инвентаризации');

dateTime 'Дата' = DATA DATETIME (Adjustment);
WHEN LOCAL SET(Adjustment a IS Adjustment) AND NOT CHANGED(dateTime(a)) DO dateTime(a) <- currentDateTime();
@defineDateTimeAggregation(Adjustment);

@defineNumber(adjustment, 'Инвентаризации', 'ИНВ');

numberDate 'Описание' (Adjustment a) = number(a) + ' от ' + dateTime(a) IN id;

DESIGN options {
    commons {
        MOVE PROPERTY(nameNumeratorAdjustment());
    }
}

location 'Место хранения' = DATA Location (Adjustment) NONULL;
nameLocation 'Место хранения' (Adjustment a) = name(location(a));

// type
CLASS AdjustmentType 'Тип инвентаризации' {
    all 'Все',
    category 'По категории',
    manual 'Вручную'
}
name 'Имя' (AdjustmentType o) = ISTRING[100](staticCaption(o)) IF o IS AdjustmentType CHARWIDTH 15;

FORM type 'Тип инвентаризации'
    OBJECTS t = AdjustmentType PANEL
    PROPERTIES(t) 'Тип инвентаризации' = name
    
    EDIT AdjustmentType OBJECT t  
;

type 'Тип' =  DATA AdjustmentType (Adjustment) NONULL;
nameType 'Тип инвентаризации' (Adjustment a) = name(type(a));

category 'Категория' = DATA Category (Adjustment);
nameCategory 'Категория' (Adjustment a) = name(category(a));

note 'Примечание' = DATA ISTRING[50] (Adjustment);

// lines
CLASS AdjustmentLine 'Строка инвентаризации';

adjustment = DATA Adjustment (AdjustmentLine) NONULL DELETE;

index '№' = PARTITION SUM 1 ORDER AdjustmentLine l BY adjustment(l) IN id MATERIALIZED CHARWIDTH 3;

product 'Товар' = DATA Product (AdjustmentLine);
nameProduct 'Товар' (AdjustmentLine l) = name(product(l));
idBarCodeProduct 'Штрихкод' (AdjustmentLine l) = idBarCode(product(l));
idProduct 'Код' (AdjustmentLine l) = id(product(l));

uom (AdjustmentLine l) = uom(product(l));
nameUom 'Ед. изм.' (AdjustmentLine l) = name(uom(l));

location 'Место хранения' = DATA Location (AdjustmentLine);
nameLocation 'Место хранения' (AdjustmentLine l) = canonicalName(location(l));

WHEN LOCAL SETCHANGED(location(adjustment(AdjustmentLine l))) AND NOT location(l) DO location(l) <- location(adjustment(l));

CONSTRAINT location(AdjustmentLine l) AND NOT level(location(l), location(adjustment(l))) CHECKED BY location[AdjustmentLine]
    MESSAGE 'Место хранения строки инвентаризации не является потомком места хранения документа';

lastAdjustmentLine (Adjustment a, Product p, Location l) = 
    GROUP LAST AdjustmentLine al ORDER al BY adjustment(al), product(al), location(al);

theoreticalQuantity 'Теоретическое кол-во' = DATA NUMERIC[16,3] (AdjustmentLine);

fillTheoreticalQuantity ABSTRACT LIST (AdjustmentLine);
fillTheoreticalQuantity(AdjustmentLine l) + {
    theoreticalQuantity(l) <- prevOnHandB(location(l), product(l), dateTime(adjustment(l)));
}
WHEN LOCAL (SETCHANGED (location(AdjustmentLine l)) OR SETCHANGED(product(l))) 
           AND NOT CHANGED(theoreticalQuantity(l)) DO
    fillTheoreticalQuantity(l); 

// line properties
nameStatus 'Статус' (AdjustmentLine l) = nameStatus(adjustment(l)) IN id;
dateTime 'Дата' (AdjustmentLine l) = dateTime(adjustment(l)) IN id;
number 'Номер' (AdjustmentLine l) = number(adjustment(l)) IN id;

edit (AdjustmentLine l) + { edit(adjustment(l)); } 

FORM adjustment 'Инвентаризация'
    OBJECTS a = Adjustment PANEL 
    PROPERTIES(a) READONLYIF readonly(a)
                  dateTime, number, 
                  nameLocation, nameType, nameCategory SHOWIF type(a) = AdjustmentType.category
    PROPERTIES(a) note 
    
    OBJECTS l = AdjustmentLine
    PROPERTIES(l) READONLYIF readonly(a) 
                  index READONLY, 
                  nameProduct, nameUom, idBarCodeProduct, idProduct,
                  nameLocation, 
                  theoreticalQuantity,
                  NEW, DELETE
    FILTERS adjustment(l) = a
    
    EDIT Adjustment OBJECT a
;

DESIGN adjustment {
    caption = (CONCAT ' ', 'Инвентаризация', '№' + number(a), 'от ' + dateTime(a));
    OBJECTS {
        NEW header {
            NEW headerCenter {
                alignment = STRETCH;
                type = CONTAINERH;
                NEW headerLeft {
                    MOVE PROPERTY(dateTime(a));
                    MOVE PROPERTY(number(a));
                }
                NEW headerRight {
                    MOVE PROPERTY(nameLocation(a))  { notNull = TRUE; }
                    MOVE PROPERTY(nameType(a)) { notNull = TRUE; }           
                    MOVE PROPERTY(nameCategory(a)) { notNull = TRUE; }           
                }
            }
            NEW headerBottom {
                alignment = STRETCH;
                MOVE PROPERTY(note(a));           
            }
        }
        NEW details {
            fill = 1;
            type = TABBED;
            NEW lines {
                caption = 'Строки';
                type = SPLITH;
                MOVE BOX(l);
                NEW linesTab {
                    fill = 0.3;
                    type = TABBED;
                }
            }
        }
    }
}

@defineDocHistory(adjustment, a, product, theoreticalQuantity);

FORM adjustments 'Инвентаризации'
    OBJECTS a = Adjustment
    PROPERTIES(a) READONLY dateTime, number, nameType, nameLocation, nameCategory, note
    PROPERTIES(a) NEWSESSION NEW, EDIT, DELETE 
    
    LIST Adjustment OBJECT a
;

@defineDocStatusForm(adjustment, a);
@defineDocLinesCount(adjustment, a);
@defineDocLocationAccess(adjustment, a);

@defineDateFilterForm(adjustments, a);

NAVIGATOR {
    operations {
        NEW adjustments;
    }
}

// fill
fill 'Заполнить' (Adjustment a) {
    FOR adjustment(AdjustmentLine l) = a DO
        fillTheoreticalQuantity(l);

    FOR (level(Location loc, location(a)) AND onHand(loc, Product p)) AND 
        (level(category(p), category(a)) OR NOT type(a) = AdjustmentType.category) AND 
        NOT type(a) = AdjustmentType.manual AND NOT lastAdjustmentLine(a, p, loc) ORDER name(p), p DO NEW l = AdjustmentLine {
            adjustment(l) <- a;
            product(l) <- p;
            location(l) <- loc;
    }     
}

EXTEND FORM adjustment
    PROPERTIES(a) fill SHOWIF location(a) AND NOT readonly(a)
;

DESIGN adjustment {
    secondaryActions {
        MOVE PROPERTY(fill(a));
    }
}