MODULE AdjustmentPrint;

REQUIRE AdjustmentCanceled;

NAMESPACE Inventory;

company 'Компания' (Adjustment a) = company(location(a));
nameCompany 'Компания' (Adjustment a) = name(company(a));

addressLinesCompany 'Адрес' (Adjustment a) = addressLines(company(a)); 
phoneCompany 'Телефон' (Adjustment a) = phone(company(a));
emailCompany 'Электронная почта' (Adjustment a) = email(company(a));
websiteCompany 'Веб сайт' (Adjustment a) = website(company(a));

FORM printAdjustment 'Результат инвентаризации'
    OBJECTS a = Adjustment PANEL 
    PROPERTIES(a) nameStatus, dateTime, nameType, nameCompany, addressLinesCompany, phoneCompany, emailCompany, websiteCompany
    
    OBJECTS l = AdjustmentLine
    PROPERTIES(l) nameLocation, nameProduct, realQuantity
    FILTERS adjustment(l) = a
;

print(Adjustment a) {
    PRINT printAdjustment OBJECTS a = a; 
}

EXTEND FORM adjustment
    PROPERTIES(a) print
;

DESIGN adjustment {
    secondaryActions {
        MOVE PROPERTY(print(a)) { caption = 'Печать'; charWidth = 10; }
    }
}
