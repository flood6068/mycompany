MODULE ShipmentWaiting;

REQUIRE Shipment, ShipmentSelection, InvLedger;

NAMESPACE Inventory;

EXTEND CLASS ShipmentStatus {
    waiting 'В ожидании'
}
waiting 'В ожидании' = DATA BOOLEAN (Shipment);

status (Shipment s) += WHEN waiting(s) THEN ShipmentStatus.waiting;
colorStatus (Shipment s) += WHEN status(s) = ShipmentStatus.waiting THEN RGB(250,150,157);

markAsToDo 'В работу' (Shipment s) {
    APPLY; 
    IF canceled() THEN RETURN;
    
    NEWSESSION {
        waiting(s) <- TRUE;
        APPLY;
    }
}

EXTEND FORM shipment
    PROPERTIES(s) markAsToDo SHOWIF status(s) = ShipmentStatus.draft AND NOT immediate(s), waiting 
;

DESIGN shipment {
    primaryActions {
        MOVE PROPERTY(markAsToDo(s)) { fontStyle = 'bold'; }
    }                
    status {
        MOVE PROPERTY(waiting(s));                               
    }
}

EXTEND FORM shipments    
    EXTEND FILTERGROUP status
        FILTER 'В ожидании' status(s) = ShipmentStatus.waiting    
;

@defineSelectionAction(shipments, Shipment, markAsToDo, 'В работу', statuses);

// total
waiting 'Ожидает отгрузку' (Location l, Product p) = 
    GROUP SUM initialDemand(ShipmentLine sl) IF status(shipment(sl)) = ShipmentStatus.waiting AND 
              location(sl) = l AND product(sl) = p;

waitingRec 'Ожидает отгрузку' (Location l, Product p) =
    GROUP SUM waiting(Location child, p) IF level(child, l);
