MODULE ShipmentCanceled;

REQUIRE ShipmentDone;

NAMESPACE Inventory;
    
EXTEND CLASS ShipmentStatus {
    canceled 'Отменен'
}
canceled 'Отменен' = DATA BOOLEAN (Shipment);
active 'Активен' (Shipment s) = done(s) AND NOT canceled(s);

status(Shipment s) += WHEN canceled(s) THEN ShipmentStatus.canceled;
colorStatus(Shipment s) += WHEN status(s) = ShipmentStatus.canceled THEN RGB(181,195,247);

cancel 'Отменить' (Shipment s) {
    APPLY; 
    IF canceled() THEN RETURN;
           
    NEWSESSION {
        canceled(s) <- TRUE;
        APPLY;
    }
}

EXTEND FORM shipment
    PROPERTIES(s) cancel SHOWIF NOT (status(s) = ShipmentStatus.draft OR status(s) = ShipmentStatus.canceled), canceled 
;

DESIGN shipment {
    secondaryActions {
        MOVE PROPERTY(cancel(s));
    }             
    status {
        MOVE PROPERTY(canceled(s));                               
    }    
}

EXTEND FORM shipments    
    EXTEND FILTERGROUP status
        FILTER 'Отменен' status(s) = ShipmentStatus.canceled    
;

@defineSelectionAction(shipments, Shipment, cancel, 'Отменить', statuses);