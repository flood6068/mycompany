MODULE InventoryValuation;

REQUIRE CostLedger, LocationEmployee, BarCode;

NAMESPACE Inventory;

FORM inventoryValuation 'Себестоимость остатков'
    OBJECTS lp = (l = Location, p = Product)
    PROPERTIES READONLY       nameCostingMethod(p), 'Склад' = canonicalName(l)
    PROPERTIES(p) READONLY    'Товар' = name, nameUom, idBarCode, id
    PROPERTIES(l, p) READONLY costQuantity, costPrice, costAmount
    PROPERTIES(p) NEWSESSION EDIT
    PROPERTIES recalculateCost() DRAW lp TOOLBAR
    FILTERS accessGranted(l)
    FILTERGROUP rowFilter
        FILTER 'С остатком' costQuantity(l, p)
        FILTER 'С движением' countLedger(l, p)
        
    OBJECTS ol = OutCostLedger
    PROPERTIES(ol) READONLY class, type, dateTime, number, nameLocation, nameProduct, quantity, costed, price, amount
    PROPERTIES(ol) NEWSESSION EDIT
    FILTERS location(ol) = l, product(ol) = p
    
    OBJECTS il = InCostLedger
    PROPERTIES(il) READONLY class, type, dateTime, number, nameLocation, nameProduct, quantity, costed, price, amount
    PROPERTIES(il) NEWSESSION EDIT
    PROPERTIES READONLY quantity(ol, il)
    FILTERS location(il) = l, product(il) = p
    FILTERGROUP filterIn
        FILTER 'По расходу' quantity(ol, il)
    
    OBJECTS locProd = (location = Location, product = Product) PIVOT
    PROPERTIES READONLY 'Склад' = canonicalName(location), 'Товар' = name(product), 
                        level1(product), level2(product), level3(product), level4(product), 'Группа полная' = canonicalNameCategory(product),
                        costPrice(location, product)
    PROPERTIES READONLY MEASURE costQuantity(location, product), costAmount(location, product)
    FILTERS costQuantity(location, product)
;

DESIGN inventoryValuation {
    OBJECTS {
        NEW header {
            fill = 1;
            type = TABBED;
            NEW valuation {
                caption = 'Себестоимость остатков';
                MOVE BOX (lp);
                NEW details {
                    fill = 1;
                    type = SPLITH;
                    MOVE BOX(ol);
                    MOVE BOX(il);
                }
            }
            NEW report {
                caption = 'Отчет';
                MOVE BOX (locProd);
            }
        }
    }
}

NAVIGATOR {
    reporting {
        NEW inventoryValuation;
    }
}
