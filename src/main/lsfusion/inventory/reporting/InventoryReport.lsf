MODULE InventoryReport;

REQUIRE InvLedger, ResLedger, ItemImage, ItemAttribute, LocationEmployee;

NAMESPACE Inventory;

selectedDateTime 'Выбрать дату' = DATA LOCAL DATETIME () PREREAD;
singleLocation 'По одному складу' = DATA LOCAL BOOLEAN () PREREAD;
onHandDateTime 'Остаток на дату' (Location l, Product p) = onHand(l, p, selectedDateTime());
                                
FORM inventoryReport 'Текущие остатки'
    PROPERTIES selectedDateTime(), singleLocation()
    
    TREE locations lt = Location PARENT parent(lt)
    PROPERTIES READONLY name(lt)
    ORDERS name(lt)
    
    TREE categories c = Category PARENT parent(c)
    PROPERTIES READONLY name(c)
    ORDERS name(c)
    
    OBJECTS lp = (l = Location, p = Product)
    PROPERTIES READONLY 'Склад' = canonicalName(l) SHOWIF NOT singleLocation(), 'Товар' = name(p)
    PROPERTIES(l, p) READONLY reserved, available
    PROPERTIES(l, p) READONLY SHOWIF NOT selectedDateTime() onHand, onHandRec
    PROPERTIES(l, p) READONLY SHOWIF selectedDateTime() onHandDateTime 
    PROPERTIES(l, p) READONLY dateReceived, dateShipped
    PROPERTIES(p) READONLY image PANEL
    PROPERTIES(p) EDIT
    FILTERS IF singleLocation() THEN l = lt ELSE isChild(l, lt),
            level(category(p), c),
            IF selectedDateTime() THEN onHandDateTime(l, p) ELSE 
                (IF singleLocation() THEN onHandRec(l, p) ELSE onHand(l, p)),
            accessGranted(l)
                
    OBJECTS il = InvLedger
    PROPERTIES(il) READONLY class, type, dateTime, number, namePartner,
                            nameFromLocation, onHandFromLocation, 
                            nameToLocation, onHandToLocation,
                            quantity
    PROPERTIES(il) EDIT
    FILTERGROUP activeInv
        FILTER 'Активный' active(il) DEFAULT
    FILTERS product(il) = p,
            IF singleLocation() THEN (level(fromLocation(il), l) OR level(toLocation(il), l))
                                ELSE (fromLocation(il) = l OR toLocation(il) = l),
            dateTime(il) <= selectedDateTime() OR NOT selectedDateTime()

    OBJECTS rl = ResLedger
    PROPERTIES(rl) READONLY class, type, dateTime, number, 
                            namePartner, nameLocation, 
                            reserved
    PROPERTIES(rl) EDIT
    FILTERS product(rl) = p,
            IF singleLocation() THEN (TRUE IF level(location(rl), l))
                                ELSE location(rl) = l,
            dateTime(il) <= selectedDateTime() OR NOT selectedDateTime()
            
    OBJECTS a = ItemAttribute
    OBJECTS rlp = (ll = Location, rp = Product) PIVOT
    PROPERTIES READONLY 'Склад' = canonicalName(ll), 'Товар' = name(rp), value(rp, a) COLUMNS (a) HEADER name(a),
                        level1(rp), level2(rp), level3(rp), level4(rp), 'Группа полная' = canonicalNameCategory(rp)
    PROPERTIES(ll, rp) READONLY dateReceived, dateShipped
    PROPERTIES READONLY MEASURE reserved(ll, rp), available(ll, rp), onHand 'Остаток' = IF selectedDateTime() THEN onHandDateTime(ll, rp) ELSE onHand(ll, rp)
    FILTERS IF selectedDateTime() THEN onHandDateTime(ll, rp) ELSE onHand(ll, rp),
            accessGranted(ll)
;

DESIGN inventoryReport {
    OBJECTS {
        NEW header {
            type = CONTAINERH;
            MOVE PROPERTY (selectedDateTime());
            MOVE PROPERTY (singleLocation());
        }
        NEW invReport {
            fill = 1;
            type = TABBED;
            NEW pane {
                caption = 'Текущие остатки';
                type = SPLITH;
                fill = 1;
                NEW pane1 {
                    fill = 1;
                    type = SPLITV;
                    MOVE BOX(TREE locations) { caption = 'Места хранения'; }
                    MOVE BOX(TREE categories) { caption = 'Категории'; }
                }
                NEW pane2 {
                    fill = 2;
                    type = SPLITV;
                    MOVE BOX(lp);
                    NEW tabs {
                        type = TABBED;
                        MOVE BOX(il) { caption = 'Регистр изменения остатка'; }
                        MOVE BOX(rl) { caption = 'Регистр резервов'; }
                        NEW image { 
                            caption = 'Изображение';
                            MOVE PROPERTY(image(p)) { caption = ''; fill = 1; } 
                        }
                    }
                }               
            }
            MOVE BOX (rlp) { caption = 'Отчет'; }
        }
    }
}

NAVIGATOR {
    reporting {
        NEW inventoryReport;
    }
}