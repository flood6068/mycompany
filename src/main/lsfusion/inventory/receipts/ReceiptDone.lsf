MODULE ReceiptDone;

REQUIRE ReceiptReady;

NAMESPACE Inventory;

EXTEND CLASS ReceiptStatus {
    done 'Принят'
}
done 'Принят' = DATA BOOLEAN (Receipt);

status (Receipt r) += WHEN done(r) THEN ReceiptStatus.done;

executionDateTime 'Дата выполнения' = DATA DATETIME (Receipt);
WHEN SET(done(Receipt r)) AND NOT CHANGED(executionDateTime(r)) DO executionDateTime(r) <- currentDateTime(); 
executionDateTime 'Дата выполнения' (ReceiptLine l) = executionDateTime(receipt(l));

done 'Принято' = DATA NUMERIC[16,3] (ReceiptLine);

CONSTRAINT done(ReceiptLine l) < 0 OR done(l) > initialDemand(l) 
    MESSAGE 'Принятое количество должно быть от 0 до исходного';

setDone 'Заполнить Принято' (Receipt r) {
    done(ReceiptLine l) <- initialDemand(l) WHERE receipt(l) = r;    
}

validate 'Принят' (Receipt r) {        
    APPLY; // сохраняем изменения
    IF canceled() THEN RETURN; // если сработало ограничение, то выходим
        
    NEWSESSION {
        IF NOT immediate(r) THEN {
            IF NOT GROUP SUM 1 IF done(ReceiptLine l) AND receipt(l) = r THEN
                ASK 'Не заполены принятые количества для приемки ' + number(r) + '. Проставить ожидаемыми ?' DO {
                    done(ReceiptLine l) <- initialDemand(l) WHERE receipt(l) = r;
                } ELSE RETURN;
        }
        
        done(r) <- TRUE;
        APPLY;
    }
}

toValidate (Receipt r) = status(r) = ReceiptStatus.ready OR (immediate(r) AND status(r) = ReceiptStatus.draft);

EXTEND FORM receipt
    PROPERTIES(r) validate SHOWIF toValidate(r), 
                  done,
                  setDone DRAW l TOOLBAR SHOWIF status(r) = ReceiptStatus.ready,
                  executionDateTime SHOWIF done(r)
    
    PROPERTIES(l) done BACKGROUND RGB(198,230,247) IF toValidate(r)
;

DESIGN receipt {
    primaryActions {
        MOVE PROPERTY(validate(r)) { fontStyle = 'bold'; }
    }
    status {
        MOVE PROPERTY(done(r));                              
    } 
    headerLeft {
        MOVE PROPERTY(executionDateTime(r));
    }                  
}

EXTEND FORM receipts 
    PROPERTIES(r) READONLYIF isReadonly() executionDateTime AFTER scheduledDateTime(r)

    EXTEND FILTERGROUP status
        FILTER 'Принят' status(r) = ReceiptStatus.done
;

@defineSelectionAction(receipts, Receipt, validate, 'Принят', statuses);