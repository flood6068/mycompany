MODULE ReceiptReady;

REQUIRE Receipt, ResLedger, ReceiptSelection;

NAMESPACE Inventory;

EXTEND CLASS ReceiptStatus {
    ready 'К приемке'
}
ready 'К приемке' = DATA BOOLEAN (Receipt);

status (Receipt r) += WHEN ready(r) THEN ReceiptStatus.ready;
colorStatus (Receipt r) += WHEN status(r) = ReceiptStatus.ready THEN RGB(252,247,149);

markAsToDo 'В работу' (Receipt r) {
    APPLY; 
    IF canceled() THEN RETURN;
    
    NEWSESSION {
        ready(r) <- TRUE;
        APPLY;
    }
}

EXTEND FORM receipt
    PROPERTIES(r) markAsToDo SHOWIF status(r) = ReceiptStatus.draft AND NOT immediate(r), ready 
;

DESIGN receipt {
    primaryActions {
        MOVE PROPERTY(markAsToDo(r)) { fontStyle = 'bold'; }
    }       
    status {
        MOVE PROPERTY(ready(r));                               
    }
}

EXTEND FORM receipts    
    EXTEND FILTERGROUP status
        FILTER 'К приемке' status(r) = ReceiptStatus.ready    
;

@defineSelectionAction(receipts, Receipt, markAsToDo, 'В работу', statuses);