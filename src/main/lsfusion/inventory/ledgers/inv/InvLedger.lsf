MODULE InvLedger;

REQUIRE InventorySettings, Category, Product, Location, Partner, Time;

NAMESPACE Inventory;

CLASS ABSTRACT InvLedger 'Регистр изменения остатка';
class 'Вид' (InvLedger l) = objectClassName(l) IF l IS InvLedger CHARWIDTH 15;

type 'Тип' = ABSTRACT ISTRING[100] (InvLedger) MATERIALIZED;

active 'Активный' = ABSTRACT BOOLEAN (InvLedger) MATERIALIZED;
dateTime 'Время' = ABSTRACT DATETIME (InvLedger) MATERIALIZED;
number 'Номер' = ABSTRACT STRING[31] (InvLedger) MATERIALIZED;

fromLocation 'Место хранения (откуда)' = ABSTRACT Location (InvLedger) MATERIALIZED;
nameFromLocation 'Место хранения (откуда)' (InvLedger l) = name(fromLocation(l));

toLocation 'Место хранения (куда)' = ABSTRACT Location (InvLedger) MATERIALIZED;
nameToLocation 'Место хранения (куда)' (InvLedger l) = name(toLocation(l));

product 'Товар' = ABSTRACT Product (InvLedger) MATERIALIZED;
nameProduct 'Товар' (InvLedger l) = name(product(l));

INDEX product(InvLedger l);
INDEX fromLocation(InvLedger l), product(l);
INDEX toLocation(InvLedger l), product(l);

quantity 'Кол-во' = ABSTRACT NUMERIC[16,3] (InvLedger) MATERIALIZED;

partner = ABSTRACT Partner (InvLedger) MATERIALIZED;
namePartner 'Контрагент' (InvLedger l) = name(partner(l));

// result  
inQuantity 'Кол-во поступившее' (Location ll, Product p) = GROUP SUM quantity(InvLedger l) 
                                                    IF active(l) AND toLocation(l) = ll AND product(l) = p MATERIALIZED;
                                                    
inQuantity 'Кол-во поступившее'(Location ll, Product p, DATETIME dt) = inQuantity(ll, p) (-)
    GROUP SUM quantity(InvLedger l)  IF active(l) AND toLocation(l) = ll AND product(l) = p AND dateTime(l) > dt;
                                   
outQuantity 'Кол-во отправленное' (Location ll, Product p) = GROUP SUM quantity(InvLedger l) 
                                                    IF active(l) AND fromLocation(l) = ll AND product(l) = p MATERIALIZED;

outQuantity 'Кол-во отправленное' (Location ll, Product p, DATETIME dt) = outQuantity(ll, p) (-)
    GROUP SUM quantity(InvLedger l) IF active(l) AND  fromLocation(l) = ll AND product(l) = p AND dateTime(l) > dt;

onHand 'Текущий остаток' (Location ll, Product p) = inQuantity(ll, p) (-) outQuantity(ll, p) MATERIALIZED;

onHand 'Текущий остаток' (Location ll, Product p, DATETIME dt) = 
                                                            inQuantity(ll, p, dt) (-) outQuantity(ll, p, dt) IF dt;

onHandRec 'Текущий остаток (с учетом вложенности)' (Location l, Product p) = 
    GROUP SUM onHand(Location child, p) IF level(child, l) MATERIALIZED;

// dates
dateReceived 'Дата последнего прихода' (Location l, Product p) = 
    GROUP LAST dateTime(InvLedger il) ORDER dateTime(il) WHERE toLocation(il) = l AND product(il) = p;
prevDateReceived (Location l, Product p) = PREV(dateReceived(l, p));
    
dateShipped 'Дата последней отгрузки' (Location l, Product p) = 
    GROUP LAST dateTime(InvLedger il) ORDER dateTime(il) WHERE fromLocation(il) = l AND product(il) = p;
    
datePrevOperation(Location l, Product p, DATETIME dt) = 
    GROUP LAST dateTime(InvLedger il) ORDER dateTime(il) 
    WHERE (fromLocation(il) = l OR toLocation(il) = l) AND product(il) = p AND dateTime(il) < dt AND active(il);

onHandFromLocation 'Остаток после операции (откуда)' (InvLedger inv) = onHand(fromLocation(inv), product(inv), dateTime(inv));
onHandToLocation 'Остаток после операции (куда)' (InvLedger inv)= onHand(toLocation(inv), product(inv), dateTime(inv));                                               

// constraint
positiveOnHand 'Запрет отрицательных остатков' = DATA BOOLEAN (Location) CHARWIDTH 15;
            
recPositiveOnHand 'Запрет отрицательных остатков' (Location child) = 
    GROUP LAST positiveOnHand(Location parent) ORDER DESC level(child, parent) 
    WHERE positiveOnHand(parent) MATERIALIZED CHARWIDTH 15;
                                                      
CONSTRAINT SET(onHand(Location ll, Product p) < 0) AND recPositiveOnHand(ll) 
            MESSAGE 'Текущий остаток не может быть отрицательным';
            
EXTEND FORM location
    PROPERTIES(l) positiveOnHand
;
DESIGN location {
    params {
        MOVE PROPERTY(positiveOnHand(l));
    }
}

EXTEND FORM locations
    PROPERTIES(l) READONLY recPositiveOnHand
;