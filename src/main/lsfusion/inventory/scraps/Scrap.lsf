MODULE Scrap;

REQUIRE InventorySettings, Product, DateUtils, 
        LocationEmployee, InvLedger, 
        BarCode, Doc, ScrapType;

NAMESPACE Inventory;

CLASS Scrap 'Списание';

@defineDocStatus(scrap, 'списания');

// type
type 'Тип' = DATA ScrapType (Scrap) NONULL;
nameType 'Тип' (Scrap r) = name(type(r));

WHEN LOCAL SET(Scrap r IS Scrap) AND NOT CHANGED(type(r)) DO type(r) <- defaultScrapType(); 

dateTime 'Дата' = DATA DATETIME (Scrap) NONULL;
dateTime(Scrap s) <- currentDateTime() WHEN SET(s IS Scrap);
@defineDateTimeAggregation(Scrap);

@defineNumberType(scrap);

numberDate 'Описание' (Scrap s) = number(s) + ' от ' + dateTime(s) IN id;

location 'Место хранения' = DATA Location (Scrap) NONULL;
nameLocation 'Место хранения' (Scrap s) = name(location(s));

note 'Примечание' = DATA ISTRING (Scrap) CHARWIDTH 30;

// lines
CLASS ScrapLine 'Строка списания';

scrap 'Списание' = DATA Scrap (ScrapLine) NONULL DELETE;

index '№' = PARTITION SUM 1 ORDER ScrapLine l BY scrap(l) IN id MATERIALIZED CHARWIDTH 3;

product 'Продукт' = DATA Product (ScrapLine);
nameProduct 'Продукт' (ScrapLine l) = name(product(l));
idBarCodeProduct 'Штрихкод' (ScrapLine l) = idBarCode(product(l));
idProduct 'Код' (ScrapLine l) = id(product(l));

uom (ScrapLine l) = uom(product(l));
nameUom 'Ед. изм.' (ScrapLine l) = name(uom(l));

quantity 'Кол-во' = DATA NUMERIC[16,3] (ScrapLine);

FORM scrap 'Списание'
    OBJECTS s = Scrap PANEL 
    PROPERTIES(s) nameType, dateTime, number, nameLocation, note
    
    OBJECTS l = ScrapLine
    PROPERTIES(l) index, nameProduct, nameUom, idBarCodeProduct, idProduct, quantity, NEW, DELETE
    FILTERS scrap(l) = s
    
    EDIT Scrap OBJECT s
;

DESIGN scrap {
    caption = (CONCAT ' ', 'Списание', '№' + number(s), 'от ' + dateTime(s));
    OBJECTS {
        PROPERTY(nameType(s)) { notNull = TRUE; }
        NEW details {
            fill = 1;
            type = TABBED;
            NEW lines {
                caption = 'Строки';
                type = SPLITH;
                MOVE BOX(l);
                NEW linesTab {
                    fill = 0.3;
                    type = TABBED;
                }    
            }
        }
    }
}

@defineDocHistory(scrap, s, product, quantity);

FORM scraps 'Списания'
    OBJECTS s = Scrap 
    PROPERTIES(s) READONLY nameType, dateTime, number, nameLocation, note
    PROPERTIES(s) NEWSESSION NEW, EDIT, DELETE
    
    LIST Scrap OBJECT s
;

@defineDocStatusForm(scrap, s);
@defineDocLinesCount(scrap, s);
@defineDocLocationAccess(scrap, s);
@defineDateFilterForm(scraps, s);

NAVIGATOR {
    operations {
        NEW scraps;
    }
}


// copy
copy 'Копировать' (Scrap s) {
    NEWSESSION {
        NEW ns = Scrap {
            dateTime(ns) <- dateTime(s);
            location(ns) <- location(s);
            FOR scrap(ScrapLine l) = s INLINE NEW nl = ScrapLine DO {
                scrap(nl) <- ns;
                product(nl) <- product(l);
                quantity(nl) <- quantity(l);
            }
            SHOW scrap OBJECTS s = ns DOCKED;
        }
    }
}

EXTEND FORM scrap
    PROPERTIES(s) copy
;

DESIGN scrap {
    secondaryActions {
        MOVE PROPERTY(copy(s));
    }
}