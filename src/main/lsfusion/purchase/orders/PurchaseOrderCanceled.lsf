MODULE PurchaseOrderCanceled;

REQUIRE PurchaseOrder, PurchaseOrderSelection;

NAMESPACE Purchase;

EXTEND CLASS OrderStatus {
    canceled 'Отменен'
}
canceled 'Отменен' = DATA BOOLEAN (Order);

status(Order o) += WHEN canceled(o) THEN OrderStatus.canceled;
colorStatus(Order o) += WHEN status(o) = OrderStatus.canceled THEN RGB(252,247,149);

cancel 'Отменить' (Order o) {
    APPLY; 
    IF canceled() THEN RETURN;
           
    NEWSESSION {
        canceled(o) <- TRUE;
        APPLY;
    }    
}

EXTEND FORM order
     PROPERTIES(o) cancel SHOWIF (NOT status(o) = OrderStatus.draft AND NOT status(o) = OrderStatus.canceled), canceled
;

DESIGN order {
    secondaryActions {
        MOVE PROPERTY(cancel(o));
    }
    status {
        MOVE PROPERTY(canceled(o));    
    }
}

EXTEND FORM orders    
    EXTEND FILTERGROUP status
        FILTER 'Отменен' status(o) = OrderStatus.canceled    
;

@defineSelectionAction(orders, Order, cancel, 'Отменить', statuses);