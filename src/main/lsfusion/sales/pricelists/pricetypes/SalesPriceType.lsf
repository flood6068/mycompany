MODULE SalesPriceType;

REQUIRE SalesSettings, PartnerSales, Partner; 

NAMESPACE Sales;

CLASS PriceType 'Виды цены';
name 'Наименование' = DATA ISTRING[100] (PriceType) NONULL CHARWIDTH 10;

id 'Код' = DATA STRING[20] (PriceType);
priceType = GROUP AGGR PriceType t BY id(t);

taxIncluded 'С учетом налогов' = DATA BOOLEAN (PriceType);  

FORM priceType 'Вид цены'
    OBJECTS pt = PriceType PANEL 
    PROPERTIES(pt) name, id, taxIncluded

    EDIT PriceType OBJECT pt
;

FORM priceTypes 'Виды цен'
    OBJECTS pt = PriceType
    PROPERTIES(pt) READONLY name, id, taxIncluded
    PROPERTIES(pt) NEWSESSION NEW, EDIT, DELETE 

    LIST PriceType OBJECT pt
;   

NAVIGATOR {
    settings {
        NEW priceTypes;
    }
}

priceType = DATA PriceType (Partner);
namePriceType 'Вид цен' (Partner p) =  name(priceType(p));

EXTEND FORM partner 
    PROPERTIES(p) namePriceType 
;

DESIGN partner {
    customerOptions {
        MOVE PROPERTY (namePriceType(p));
    }
}