MODULE SalesOrderLocked;

REQUIRE SalesOrderSent, SalesOrderConfirmed;

NAMESPACE Sales;

EXTEND CLASS OrderStatus {
    locked 'Закрыт'
}
locked 'Закрыт' = DATA BOOLEAN (Order);

status(Order o) += WHEN locked(o) THEN OrderStatus.locked;
colorStatus(Order o) += WHEN status(o) = OrderStatus.locked THEN RGB(255,255,255);

lock 'Закрыть' (Order o){
    APPLY; 
    IF canceled() THEN RETURN;
    
    NEWSESSION {
        locked(o) <- TRUE;
        APPLY;
    }
}

EXTEND FORM order
     PROPERTIES(o) lock SHOWIF (status(o) = OrderStatus.confirmed), locked;
;

DESIGN order {
    primaryActions {
        MOVE PROPERTY(lock(o)) { fontStyle = 'bold'; };
    }
    status {
        MOVE PROPERTY(locked(o));
    }
}

EXTEND FORM orders    
    FILTERGROUP unlocked
        FILTER 'Открыт' NOT locked(o) DEFAULT    
;

DESIGN orders {
    FILTERGROUP(unlocked) { alignment = STRETCH; }
}

@defineSelectionAction(orders, Order, lock, 'Закрыть', statuses);