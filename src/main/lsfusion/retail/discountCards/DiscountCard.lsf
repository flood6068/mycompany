MODULE DiscountCard;

REQUIRE Partner, RetailSettings;

NAMESPACE Retail;

CLASS DiscountCard 'Дисконтная карта';

customer 'Владелец' = DATA Partner (DiscountCard) NONULL;
nameCustomer 'Владелец' (DiscountCard d) = name(customer(d)) IN id;

countDiscountCard (Partner p) = GROUP SUM 1 IF customer(DiscountCard d) = p;  

@defineID(discountCard, 'Дисконтные карты', '', 6);

issueDate 'Дата выпуска' = DATA DATE (DiscountCard) NONULL;
blockDate 'Дата блокировки' = DATA DATE (DiscountCard);

issueDate(DiscountCard d) <- currentDate() WHEN SET(d IS DiscountCard);

CONSTRAINT issueDate(DiscountCard d) > blockDate(d) 
    MESSAGE 'Дата блокировки дисконтной карты не может быть раньше даты выпуска';

FORM discountCard 'Дисконтная карта'
    OBJECTS d = DiscountCard PANEL
    PROPERTIES(d) id, nameCustomer, issueDate, blockDate
    
    EDIT DiscountCard OBJECT d
;

FORM discountCards 'Дисконтные карты'
    OBJECTS d = DiscountCard
    PROPERTIES(d) READONLY id, nameCustomer, issueDate, blockDate
    PROPERTIES(d) NEWSESSION NEW, EDIT, DELETE;
;

NAVIGATOR {
    settings {
        NEW discountCards;
    }
}