MODULE SessionInvoice;

REQUIRE Session, SalesDiscountInvoice, Bill;

NAMESPACE Retail;

// invoice
invoiceType = DATA InvoiceType (Pos) NONULL;
nameInvoiceType 'Тип реализации' (Pos p) = name(invoiceType(p));

EXTEND FORM pos
    PROPERTIES(p) nameInvoiceType
;
DESIGN pos {
    PROPERTY(nameInvoiceType(p)) { notNull = TRUE; } 
}

session = DATA Session (Invoice);
numberSession 'Смена' (Invoice i) = number(session(i));

WHEN LOCAL SET(session(Invoice i)) AND NOT CHANGED(type(i)) DO
    type(i) <- invoiceType(pos(session(i)));
    
WHEN LOCAL SET(session(Invoice i)) AND NOT CHANGED(company(i)) DO
    company(i) <- company(pos(session(i)));

EXTEND FORM session
    OBJECTS i = Invoice
    PROPERTIES(i) READONLY nameStatus BACKGROUND colorStatus(i), dateTime, number, nameCustomer, 
                           'Итого' = fullAmount, 'Скидка' = discountAmount, 'К оплате' = amount
    PROPERTIES(i) NEWSESSION EDIT
    FILTERS session(i) = s
;

DESIGN session {
    tabPane {
        MOVE BOX(i);
    }
}

EXTEND FORM invoice
    PROPERTIES(i) READONLY numberSession SHOWIF session(i)
;

DESIGN invoice {
    otherInformation {
        NEW session {
            caption = 'Смена';
            MOVE PROPERTY(numberSession(i));
        }
    }
}

// bill
session = DATA Session (Bill);
numberSession 'Смена' (Bill i) = number(session(i));

EXTEND FORM session
    OBJECTS b = Bill
    PROPERTIES(b) READONLY nameStatus BACKGROUND colorStatus(i), dateTime, number, nameVendor, 
                           'К оплате' = amount
    PROPERTIES(b) NEWSESSION EDIT
    FILTERS session(b) = s
;

DESIGN session {
    tabPane {
        MOVE BOX(b);
    }
}

totalAmountInvoice 'Продано' (Session s) = GROUP SUM amount(Invoice i) IF session(i) = s MATERIALIZED ; 
countInvoices 'Продаж' (Session s) = GROUP SUM 1 IF session(Invoice i) = s MATERIALIZED;

totalAmountBill 'Возвращено' (Session s) = GROUP SUM amount(Bill b) IF session(b) = s MATERIALIZED;
countBills 'Возвратов' (Session s) = GROUP SUM 1 IF  session(Bill b) = s MATERIALIZED;

amount 'Итого' (Session s) = totalAmountInvoice(s) (-) totalAmountBill(s) MATERIALIZED;

EXTEND FORM sessions
    PROPERTIES(o) countInvoices, totalAmountInvoice,
                  countBills, totalAmountBill,
                  amount
;

EXTEND FORM bill
    PROPERTIES(b) READONLY numberSession SHOWIF session(b)
;

DESIGN bill {
    otherInformation {
        NEW session {
            caption = 'Смена';
            MOVE PROPERTY(numberSession(b));
        }
    }
}