MODULE BarCode;

REQUIRE MasterData, Item;

NAMESPACE MasterData;

CLASS BarCode 'Штрихкод';

item 'Номенклатура' = DATA Item (BarCode) NONULL DELETE;
idItem 'Код номенклатуры' (BarCode b) = id(item(b));
nameItem 'Номенклатура' (BarCode b) = name(item(b));

id 'Штрихкод' = DATA STRING (BarCode);
barCode (STRING[50] id) = GROUP AGGR BarCode b BY id(b);
itemBarCode (STRING[50] id) = item(barCode(id));

dataDefaultBarCode = DATA BarCode (Item);
calcDefaultBarCode (Item i) = GROUP LAST BarCode b ORDER DESC b WHERE item(b) = i;

defaultBarCode (Item i) = OVERRIDE dataDefaultBarCode(i), calcDefaultBarCode(i);
default 'По умолчанию' (BarCode b) = defaultBarCode(item(b)) = b CHARWIDTH 10;

barCode (Item i) = defaultBarCode(i);

idBarCode 'Штрихкод' (Item i) = id(barCode(i)) MATERIALIZED INDEXED;

changeDefault (BarCode b) {
    INPUT c = BOOLEAN DO {
        IF c THEN
            defaultBarCode(Item i) <- b WHERE i = item(b);
    }
}

setBarCode(Item i) {
    INPUT id = STRING DO {
        IF NOT barCode(i) THEN NEW b = BarCode {
            item(b) <- i;
        }
        id(BarCode b) <- id WHERE b = barCode(i); 
    }
}

EXTEND FORM item
    PROPERTIES(i) idBarCode AFTER canonicalNameCategory(i) ON CHANGE setBarCode(i)
    
    OBJECTS b = BarCode
    PROPERTIES(b) id, default ON CHANGE changeDefault(b)
    PROPERTIES(b) NEW, DELETE
    FILTERS item(b) = i
;

DESIGN item {
    column1 {
        MOVE PROPERTY(idBarCode(i));
    }
    tabs {
        NEW barcodes {
            caption = 'Штрихкоды';
            MOVE BOX(b);        
        }
    }
}

EXTEND FORM items
    PROPERTIES(i) READONLYIF isReadonly() idBarCode AFTER name(i)
;

consumedBarcode = DATA LOCAL BOOLEAN ();
barCode 'Штрихкод' = DATA LOCAL STRING () CHANGEKEY 'F4';

META defineBarCodeSearch(doc, let, line, itm, quantity)
    processBarCode ABSTRACT (###doc, ###doc##Line, STRING);
    scanBarCode (###doc d, ###doc##Line l) {
        INPUT id = STRING DO {
            IF itemBarCode(id) THEN {
                set###quantity(d, itemBarCode(id), quantity(d, itemBarCode(id)) (+) 1);
                SEEK ##doc.line = last###doc##Line(d, itemBarCode(id));
            } ELSE {
                consumedBarcode() <- NULL;
                processBarCode(d, l, id);
                IF NOT consumedBarcode() THEN
                    MESSAGE 'Штрихкод не найден';
            }
        }
    }
    
    EXTEND FORM ##doc
        PROPERTIES() barCode DRAW l TOOLBAR ON CHANGE scanBarCode(##let, l)
    ;
END
