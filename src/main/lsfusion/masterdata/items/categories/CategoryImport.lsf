MODULE CategoryImport;

REQUIRE Category;

NAMESPACE MasterData;

categoryExport 'Экспорт категорий' () {
    NEWSESSION {
        IF NOT (GROUP SUM 1 IF Category c AND c IS Category) THEN {
            NEW c = Category {
                id(c) <- '123';
                name(c) <- 'Моя категория';
            }
        }
        LOCAL f = EXCELFILE ();
        EXPORT XLSX HEADER FROM 'Код категории' = id(Category c), 'Наименование' = name(c), 'Код родительской категории' = idParent(c) TO f;        
        open(f());
    }
}

categoryImport 'Импорт категорий' () {
    LOCAL id = STRING[50] (INTEGER);
    LOCAL name = ISTRING[50] (INTEGER);
    LOCAL parent = ISTRING[50] (INTEGER);
    
    INPUT f = EXCELFILE DO {
        NEWSESSION {
            IMPORT XLS HEADER FROM f TO id = A, name = B, parent = C;
            FOR imported(INTEGER i) DO NEW c = Category {
                id(c) <- id(i);
                name(c) <- name(i);
            }
            
            FOR imported(INTEGER i) AND id(Category c) = id(i) DO {
                parent(c) <- category(parent(i));
            }
            APPLY;
            MESSAGE 'Импорт завершен';
        }
    }
}

EXTEND FORM migrationData 
    PROPERTIES categoryExport(), categoryImport()
;

DESIGN migrationData {
    commons {
        NEW category {
            type = CONTAINERH;
            caption = 'Категории';
            MOVE PROPERTY(categoryImport());
            MOVE PROPERTY(categoryExport());
        }
    }
}
