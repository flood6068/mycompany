MODULE ItemUomImport;

REQUIRE ItemUom;

NAMESPACE MasterData;

itemUomExport 'Экспорт соотношений единиц измерений' () {
    NEWSESSION {
        LOCAL f = EXCELFILE ();
        EXPORT XLSX HEADER FROM
            'Код товара' = id(Item i), 'Код единицы измерения' = id(Uom u), 'Соотношение' = ratio(i, u)
                WHERE ratio(i, u) TO f
        ; 
        open(f());
    }
}

itemUomImport 'Импорт соотношений единиц измерений' () {
    LOCAL idItem = STRING[50] (INTEGER);
    LOCAL idUom = STRING[50] (INTEGER);
    LOCAL ratio = NUMERIC[20,10] (INTEGER);
       
    INPUT f = EXCELFILE DO {
        NEWSESSION {
            IMPORT XLS HEADER FROM f TO idItem = A, idUom = B, ratio = C; 

            FOR [GROUP SUM 1 BY idItem(INTEGER i)](STRING num) AND NOT item(num) DO {
                MESSAGE 'Отсутствует номенклатура в базе данных с кодом ' + num + '. Импорт отменен';
                RETURN;
            }  

            FOR [GROUP SUM 1 BY idUom(INTEGER i)](STRING num) AND NOT item(num) DO {
                MESSAGE 'Отсутствует единица измерения в базе данных с кодом ' + num + '. Импорт отменен';
                RETURN;
            }  
            
            FOR id(Item i) = idItem(INTEGER j) AND id(Uom u) = idUom(j) DO
                ratio(i, u) <- ratio(j);
            
            APPLY;
            MESSAGE 'Импорт завершен';
        }
    }    
}

EXTEND FORM migrationData 
    PROPERTIES itemUomExport(), itemUomImport()
;

DESIGN migrationData {
    commons {
        NEW attribute {
            type = CONTAINERH;
            caption = 'Соотношения единиц измерений';
            MOVE PROPERTY(itemUomExport());
            MOVE PROPERTY(itemUomImport());
        }
    }
}