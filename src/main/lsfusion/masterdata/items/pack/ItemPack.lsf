MODULE ItemPack;

REQUIRE ItemUom;

NAMESPACE MasterData;

META defineItemPack(prefix)

    // category
    prefix##Uom = DATA Uom (Category);
    name###prefix##Uom 'Единица измерения для закупки' (Category c) = name(prefix##Uom(c));
    
    parent###prefix##Uom (Category child) =
       GROUP LAST prefix##Uom(Category parent) ORDER DESC level(child, parent) WHERE prefix##Uom(parent);
    
    EXTEND FORM category
        PROPERTIES(c) name###prefix##Uom
    ;
    
    DESIGN category {
        params {
            MOVE PROPERTY(name###prefix##Uom(c));
        }
    }
    
    // item   
    data###prefix##Uom = DATA Uom (Item);
    prefix##Uom 'Надбавка, %' (Item i) = OVERRIDE data###prefix##Uom(i), parent###prefix##Uom(category(i)) MATERIALIZED;
    name###prefix##Uom 'Единица измерения для закупки' (Item i) = name(prefix##Uom(i));
    
    EXTEND FORM item
        PROPERTIES(i) name###prefix##Uom
    ;
    
    DESIGN item {
        prefix {
            MOVE PROPERTY(name###prefix##Uom(i));
        }
    }
    
    prefix##Pack##Ratio 'Кол-во в месте' (Item i) = ratio(i, prefix##Uom(i)) MATERIALIZED;

END

META defineDocPack(doc, prefix, let)

    name###prefix##Uom (###doc##Line l) = name###prefix##Uom(item(l));
    packRatio 'Кол-во в месте' (###doc##Line l) = prefix##PackRatio(item(l));
    
    packQuantity 'Кол-во мест' = DATA NUMERIC[16,3] (###doc##Line);
    WHEN LOCAL (CHANGED(quantity(###doc##Line l)) OR CHANGED(item(l))) AND packRatio(l) AND NOT CHANGED(packQuantity(l)) DO
        packQuantity(l) <- NUMERIC[16,3](quantity(l) / packRatio(l));
    
    changePackQuantity (###doc##Line l) {
        INPUT n = packQuantity(l) CHANGE DO {
            quantity(l) <- NUMERIC[16,3](packQuantity(l) * packRatio(l)) WHERE packRatio(l);
        }
    }
    
    pack 'Показывать количество мест' = DATA BOOLEAN (###doc##Type);
    EXTEND FORM doc##Type PROPERTIES(o) pack;
    
    EXTEND FORM doc
        PROPERTIES(l) SHOWIF pack(type(let)) BEFORE quantity(l) BACKGROUND RGB(255,255,212)
                      packQuantity ON CHANGE changePackQuantity(l),
                      packRatio READONLY
    ;
    
END

META defineDocSearchPack(doc, prefix, let)    

    packQuantity 'Кол-во мест' (###doc o, Item i) = 
        GROUP SUM packQuantity(###doc##Line l) BY doc(l), item(l);
    
    changePackQuantity (###doc o, Item i) {
        INPUT q = NUMERIC[14,3] DO
            setQuantity(o, i, q * prefix##PackRatio(i));
    }
    
    EXTEND FORM doc
        PROPERTIES SHOWIF pack(type(let)) BEFORE quantity(let, itm) BACKGROUND RGB(255,255,212)
                   packQuantity(let, itm) READONLYIF NOT prefix##PackRatio(itm) ON CHANGE changePackQuantity(let, itm),
                   prefix##PackRatio(itm) READONLY
    ;
    
END