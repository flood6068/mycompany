MODULE LegalEntity;

REQUIRE Partner, MasterData;

NAMESPACE MasterData;

CLASS LegalEntity 'Организация';

name 'Наименование' = DATA ISTRING[50] (LegalEntity) CHARWIDTH 15 NONULL;

dataFullName = DATA ISTRING[200] (LegalEntity) CHARWIDTH 25;
fullName 'Полное наименование' (LegalEntity l) = OVERRIDE dataFullName(l), name(l);  

website 'Веб сайт' = DATA ISTRING[30](LegalEntity);

// Наследуем организацию от контрагента
EXTEND CLASS LegalEntity : Partner;

name (LegalEntity l) += name(l);
fullName (LegalEntity l) += fullName(l);

EXTEND CLASS PartnerType {
    legalEntity 'Организация'
}

type (Partner p) += WHEN p IS LegalEntity THEN PartnerType.legalEntity;

EXTEND FORM partner
    PROPERTIES(p) name[LegalEntity], fullName, website
;

DESIGN partner {
    headerDetails {
        MOVE PROPERTY(name(p));
        MOVE PROPERTY(fullName(p));
    }
    headerColumn2 {
        MOVE PROPERTY(website(p));
    }
}

newLegalEntity 'Организация' () {
    NEWSESSION {
        NEW l = LegalEntity {
            DIALOG partner OBJECTS p = l DOCKED DO seek(l);
        }
    }
} IMAGE 'add.png' CHANGEKEY 'INSERT' HIDE;

EXTEND FORM partners
    PROPERTIES newLegalEntity() DRAW p TOOLBAR
    
    LIST LegalEntity OBJECT p
;
EXTEND FORM dialogPartners
    FILTERGROUP type
        FILTER 'Организации' p IS LegalEntity

    PROPERTIES newLegalEntity() DRAW p TOOLBAR
;
