MODULE CryptoPro;

REQUIRE Company, SystemEvents, Utils;

signature = DATA LOCAL RAWFILE ();
encodedSignature = encode(signature(), 'base64');

sign INTERNAL 'org.mycompany.region.ru.gov.cryptopro.SignAction' (RAWFILE, BOOLEAN, STRING, STRING, STRING, STRING);

keyAlias 'Имя ключа УКЭП' = DATA STRING(Company);
keyPassword 'Пароль ключа УКЭП' = DATA STRING(Company) ECHO;

sign (RAWFILE f, BOOLEAN detached, STRING keyAlias, STRING keyPassword) { 
    sign(f, detached, NULL, NULL, keyAlias, keyPassword); 
}
sign (RAWFILE f, BOOLEAN detached, Company c) { 
    sign(f, detached, NULL, NULL, keyAlias(c), keyPassword(c)); 
}
sign (FILE f, BOOLEAN detached, Company c) { 
    sign(RAWFILE(f), detached, c); 
}


EXTEND FORM integrationData
    OBJECTS crc = Company
    PROPERTIES(crc) name READONLY, keyAlias, keyPassword
;

DESIGN integrationData {
    pane {
        NEW cryptopro {
            caption = 'УКЭП';
            MOVE BOX(crc);
        }
    }
}

// нужно сделать так, чтобы JCP добавился как провайдер раньше чем первый раз пошло обращение к BouncyCastle.
// раскомментировать, если BouncyCastle инициализируется раньше другими библиотеками
//initCryptoPro INTERNAL <{ new org.mycompany.region.ru.gov.cryptopro.CryptoPro(); }>;
//onStarted() + {
//    initCryptoPro();
//}